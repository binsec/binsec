# Initializations

esp := 0x32ff5c
@[esp, 4] := 0x7b454cef as return_address

@[<.text>, 512] from file
@[<.data>, 512] from file

# Import table

@[0x402058, 4] := 0x7b431a7c as GetStdHandle
@[0x402064, 4] := 0x7b442ef0 as WriteFile
@[0x402068, 4] := 0x7b442e00 as ReadFile

@[0x40204c, 4] := 0x7b432058 as LoadLibraryA
@[0x402050, 4] := 0x7b44fe10 as GetProcAddress
@[0x402054, 4] := 0x7b43175c as GetLastError
@[0x40205c, 4] := 0x7b430794 as AttachConsole
@[0x402060, 4] := 0x7b43b540 as WriteConsoleA

abort at LoadLibraryA, GetProcAddress, GetLastError, AttachConsole, WriteConsoleA

# Dynamic function mocks

## GetStdHandle

replace GetStdHandle by
  caller<32> := @[esp, 4]
  nStdHandle<32> := @[esp + 4, 4]
  case nStdHandle is
    -10: eax := 23
    -11: eax := 27
      _: assert false
  end
  esp := esp + 4
  jump at caller

## WriteFile

replace WriteFile by
  caller<32> := @[esp, 4]
  hFile<32> := @[esp + 4, 4]
  # we ignore lpBuffer
  nNumberOfBytesToWrite<32> := @[esp + 12, 4]
  lpNumberOfBytesWritten<32> := @[esp + 16, 4]
  lpOverlapped<32> := @[esp + 20, 4]
  assert hFile = 27
  assert lpOverlapped = 0
  if lpNumberOfBytesWritten <> 0 then
    @[lpNumberOfBytesWritten, 4] := nNumberOfBytesToWrite
  end
  eax := 1
  esp := esp + 4
  jump at caller

## ReadFile

replace ReadFile by
  caller<32> := @[esp, 4]
  hFile<32> := @[esp + 4, 4]
  lpBuffer<32> := @[esp + 8, 4]
  nNumberOfBytesToRead<32> := @[esp + 12, 4]
  lpNumberOfBytesRead<32> := @[esp + 16, 4]
  lpOverlapped<32> := @[esp + 20, 4]
  # assume ReadFile is only used to read in STD_INPUT_HANDLE
  assert hFile = 23
  assert lpOverlapped = 0
  # create a symbolic value and add a constraint to the number of read bytes
  nNumberOfBytesRead<32> := nondet
  assume 2 <= nNumberOfBytesRead <= nNumberOfBytesToRead
  for i<32> in 0 to nNumberOfBytesRead - 3 do
    # create a new symbolic byte in the byte stream bRead
    @[lpBuffer + i] := nondet as bRead
    # such that it is printable
    assume " " <= bRead <= "~"
  end
  # mimic the 'enter' keystroke
  @[lpBuffer + i] := "\r"
  @[lpBuffer + i + 1] := "\n"
  # write the result in the pointer passed in argument, if any
  if lpNumberOfBytesRead <> 0 then
    @[lpNumberOfBytesRead, 4] := nNumberOfBytesRead
  end
  # return a non zero value
  eax := 1
  esp := esp + 4
  jump at caller
  
# Goals

reach WriteFile such that @[@[esp + 8, 4], 15] = "You are success"
then print ascii stream bRead

cut at WriteFile if @[@[esp + 8, 4], 15] = "You are failure"
cut at return_address