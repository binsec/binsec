starting from core

# the adress with the final check for the flag
final_test_addr<64> := 0x5555555552b2

#the function that does all the uc_mem_write and _uc_emu_start
play_arm_code<64> := 0x555555555410

#We'll store ou processors here
processor<64> := 0x561ddc6532a0

#And the values of the X0 register here
x0_val<64> := 0x561ddca0bf80

proc_count<64> := 0

replace <uc_open> by
    resultp<64> := rdx
    @[resultp, 8] := processor + (8 * proc_count)
    proc_count := proc_count + 1
    return 0
end

replace <uc_mem_map>, <uc_close> by
    emul<64> := rdi
    proc_num<64> := (emul - processor) lsr 3
    assert 0 <= proc_num < 8
    return 0
end

replace <uc_reg_write> by
    emul<64> := rdi
    proc_num<64> := (emul - processor) lsr 3
    assert 0 <= proc_num < 8
    regid<32> := esi
    assert regid = 199
    valuep<64> := rdx
    @[x0_val + (8 * proc_num), 8] := @[valuep, 8]
    return 0
end

replace <uc_reg_read> by
    emul<64> := rdi
    proc_num<64> := (emul - processor) lsr 3
    assert 0 <= proc_num < 8
    regid<32> := esi
    assert regid = 199
    valuep<64> := rdx
    @[valuep, 8] := @[x0_val + (8 * proc_num), 8]
    return 0
end

# we are not supposed to use these functions, since the calling function is overriden
abort at <uc_mem_write>, <uc_emu_start>

replace <__isoc99_scanf> by
    format<64> := rdi
    assert @[format, 4] = "%lu"z
    resultp<64> := rsi
    @[resultp, 8] := nondet as int_input
    return 0
end

replace play_arm_code by
    proc_num<64> := rdi
    assert 0 <= proc_num < 8
    x0<64> := @[x0_val + (8 * proc_num), 8]
    case proc_num is
        0: x0 := (x0 * 2312467120387208231) + 2029359404220806306
        1: x0 := (x0 * 3797237591153402299) + 47731148168384596
        2: x0 := (x0 * 9790716657430116755) + 5260569239827236637
        3: x0 := (x0 * 14017540565495990065) - 2371969477855754209
        4: x0 := (x0 * 13339645624979120617) + 4567841864047838471
        5: x0 := (x0 * 13151343613722501697) + 9017076464336013363
        6: x0 := (x0 * 2982306705022426789) - 1131089788378275154
        7: x0 := (x0 * 2193733482345424343) - 1643298469197263812
         _: assert false
    end
    @[x0_val + (8 * proc_num), 8] := x0
    return 0
end

reach final_test_addr such that rbp = 0 then print model
cut at final_test_addr
